Flow co.acme.password
     Basepath ""
// Get authentication service reference from the IDP
authServiceRef = Call io.jans.as.server.service.AuthenticationService#class 
// Get CdiUtil service reference from the IDP
cdiUtilRef = Call io.jans.service.cdi.util.CdiUtil#bean authServiceRef
// Empty Result object
authResult = {}
// Retry 3 times get correct username and password 
Repeat 3 times max
     // loads the login page for username and password input
     creds = RRF "login.ftlh" authResult
     // validate username and password
     authResult.success = Call cdiUtilRef authenticate creds.username creds.password
     // Keep the username if the user need to retry authentication 
     authResult.uid = creds.username
     // check if the authentication was successful 
     When authResult.success is true
          // Return username in case of successful authentication 
          Finish authResult.uid
// Max number of failed authentication attempts reached. return false to and the flow 
it_teflv = {success:false, error: "Maximum authentication attempts reached. Authentication failed."}
Finish it_teflv